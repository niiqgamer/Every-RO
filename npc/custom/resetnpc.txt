//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
prontera,150,193,4	script	พนักงานรีเซ็ต	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	100000,	100000,	190000,	0;
	mes "[พนักงานรีเซ็ต]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Sorry you can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life.";
		close;
	}
	mes "";
	mes "ฉันคือพนักงานรีเซ็ต";
	mes "Reset ^FF0000สกิล: "+ callfunc("F_InsertComma",.@Reset[0]) +"z^000000";
	mes "Reset ^0000FFสเตตัส: "+ callfunc("F_InsertComma",.@Reset[1]) +"z^000000";
	mes "Reset ^00AA00ทั้งคู่: "+ callfunc("F_InsertComma",.@Reset[2]) +"z^000000";
	if(.@Reset[3]) mes "You may only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+", so use "+((.@Reset[3]>1)?"them":"it")+" wisely.";
	mes "คุณต้องการรีเซ็ตอะไรหล่ะ";
	next;
	set .@i,(select(" Reset ^FF0000สกิล^000000: Reset ^0000FFสเตตัส^000000: Reset ^00AA00ทั้งคู่^000000:Cancel"));
	if(.@i > 3) close;
	mes "[พนักงานรีเซ็ต]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "";
		mes "คุณไม่มี Zeny เลยนี่นา...";
		close;
	}
	if(.@Reset[3]){
		mes "";
		mes "You can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life, are you sure?";
		if(select("Let me think:That's fine") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "";
	mes "เสร็จเรียบร้อยแล้วจ้า...";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
	
OnInit:
	// ตั้งค่าข้อความป้ายบนหัว NPC
	waitingroom "รีเซ็ต Stat / Skill",0;
}

hero_lb,153,171,4	duplicate(พนักงานรีเซ็ต)	พนักงานรีเซ็ต#herolb	3570
