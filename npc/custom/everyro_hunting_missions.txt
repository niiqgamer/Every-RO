//===== rAthena Script =======================================
//= Hunting Missions
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= Random hunting missions.
//= Rewards are based on quest difficulty.
//= 
//= NOTE: Requires SQL mob database.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Small improvements and fixes.
//= 1.2 Added party support and replaced blacklists with an
//=     SQL query, both thanks to AnnieRuru.
//= 1.3 Re-added a blacklist adapted for the SQL query.
//= 1.3a Added mission reset options.
//= 1.3b Function updates.
//= 1.4 Check for deleted characters, thanks to AnnieRuru.
//=     Syntax updates and style cleaning.
//============================================================

hero_lb,159,165,6	script	Monster Hunting#bw	784,{
function Chk;
	mes "[Monster Hunting]";
	mes "";
	mes "สวัสดี " + strcharinfo(0) + "!";
	if (!#Mission_Delay) {
		next;
		mes "[Hunting Missions]";
		mes "";
		mes "สวัสดี เจ้า ...... เอ๊ะ มาใหม่หรือนั่น? ขอเวลาข้าลงทะเบียนให้เจ้าแปปนะ แล้วมาคุยกับข้าใหม่ด้วยหล่ะ";
		emotion ET_HUK;
		next;
		callsub Mission_Info;
		emotion ET_GO;
		#Mission_Delay = 1;
		close;
	}
//	mes F_Rand("Working hard, as always...", "Not slacking, I hope...");
	mes "มาคุยกับข้าวันนี้ เจ้าอยากจะรับภารกิจไปทำแก้เซ็งมั้ยหล่ะ";
	mes " ";
	mes "^FF0000~ ภารกิจที่ทำไปแล้ว = " + F_InsertPlural(Mission_Total,"Mission",0,"^00AA00%d^FF0000 %s") + ". ~^000000";
	next;
	switch(select(
		((!Mission0) ? " ^00AA00~ รับภารกิจใหม่^000000::" : ": ^FF6600~ รายงานภารกิจ^000000: ^FF0000~ ยกเลิกภารกิจ^000000") +
		": ~ ข้อมูล: ^0066FF~ ร้านค้าแต้มภารกิจ^000000: ^AA00FF~ อันดับนักล่ามือฉมัง^000000: ~ ไปดีกว่า"
	)) {
	case 1:
		mes "[Monster Hunting]";
		if (#Mission_Count) {
			mes "";
			mes "เหมือนว่าเจ้าจะรับภารกิจที่ตัวละครอื่นอยู่นะ ไปทำให้เสร็จก่อนเถอะ";
			if (!@hm_char_del_check) {  // check for deleted character
				query_sql("SELECT 1 FROM `char_reg_num` WHERE `key` = 'Mission0' AND `char_id` IN(SELECT `char_id` FROM `char` WHERE `account_id` = " + getcharid(3) + ")", .@i);
				if (!.@i) {
					next;
					mes "[Hunting Missions]";
					mes "";
					mes "^FF0000แต่เดี๋ยวก่อนนะ!!!^000000";
					mes "";
					mes "ข้าลองเช็คอีกรอบแล้ว ตัวละครที่เคยรับภารกิจไว้ ได้สาบสูญไปจากโลกใบนี้เสียแล้ว (ลบตัวละครไปแล้วสินะ) ดังนั้น ข้าจะให้โอกาสเจ้าได้รับภารกิจใหม่อีกครั้ง";
					emotion ET_SCRATCH;
					#Mission_Count = 0;
				}
				@hm_char_del_check = true;
			}
			close;
		}
		if (#Mission_Delay > gettimetick(2) && .Delay) {
			mes "เจ้าต้องอดทดให้เป็นนะ รออีกสัก " + Time2Str(#Mission_Delay) + " ค่อยมารับภารกิจใหม่จากข้า";
			close;
		}
		mes "ภารกิจวันนี้คือ:";
		query_sql("SELECT ID FROM `" + .mob_db$ + "` WHERE left(name_aegis, 4) != 'meta' AND left(name_aegis, 2) != 'E_' AND base_exp > 0 AND job_exp > 0 AND (class != 'boss' OR class is null) AND (drop3_item like '%_card' OR drop4_item like '%_card' OR drop5_item like '%_card' OR drop6_item like '%_card' OR drop7_item like '%_card' OR drop8_item like '%_card' OR drop9_item like '%_card' OR drop10_item like '%_card') AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT " + .Quests, .@mob);
		
		for (.@i = 0; .@i < .Quests; .@i++) {
			setd "Mission" + .@i, .@mob[.@i];
			setd "Mission" + .@i +"_", 0;
		}
		#Mission_Count = rand(.Count[0], .Count[1]);
		callsub Mission_Status;
		next;
		mes "[Monster Hunting]";
		mes "";
		mes "กลับมารายงานข้าด้วยหล่ะ หลังจากเจ้าทำภารกิจเสร็จแล้ว";
		mes "ขอให้โชคดีเด้อ!";
		close;
	case 2:
		mes "[Monster Hunting]";
		mes "";
		mes "รายงานภารกิจ:";
		callsub Mission_Status;
		close;
	case 3:
		mes "[Monster Hunting]";
		mes "";
		mes "แน่ใจนะที่จะยกเลิกภารกิจ";
		if (.Reset < 0 && .Delay)
			mes "แต่เวลารอรับภารกิจใหม่ จะไม่รีเซ็ตนะ";
		else if (.Reset > 0)
			mes "มีค่าใช้จ่ายนิดหน่อย ละกัน สัก " + F_InsertComma(.Reset) + " Zeny โอเคมั้ยหล่ะ?";
		next;
		switch(select(" ^FF0000~ ยกเลิกภารกิจ...^000000: ~ ข้ากดผิดหน่ะ")) {
		case 1:
			if (.Reset > 0) {
				if (Zeny < .Reset) {
					mes "[Monster Hunting]";
					mes "";
					mes "เหมือนว่าเจ้าจะมีเงินไม่พอ ที่จะยกเลิกภารกิจนะ";
					emotion ET_SORRY;
					close;
				}
				Zeny -= .Reset;
				emotion ET_MONEY;
			}
			mes "[Monster Hunting]";
			mes "";
			mes "จัดไป คุณได้ยกเลิกภารกิจแล้วเรียบร้อย";
			specialeffect2 155;
			specialeffect2 466;
			for (.@i = 0; .@i < .Quests; .@i++) {
				setd "Mission"+.@i, 0;
				setd "Mission"+.@i+"_", 0;
			}
			#Mission_Count = 0;
			if (.Reset < 0 && .Delay)
				#Mission_Delay = gettimetick(2) + (.Delay * 3600);
			close;
		case 2:
			mes "[Monster Hunting]";
			mes "";
			mes "ว่าแล้วหล่ะ เจ้าต้องไม่อยากยกเลิกภารกิจอันมีค่านี้ไปแน่นอน";
			emotion ET_SMILE;
			close;
		}
	case 4:
		callsub Mission_Info;
		close;
	case 5:
		mes "[Monster Hunting]";
		mes "";
		mes "คุณมี Mission Points ทั้งหมด ^0055FF" + #Mission_Points + "^000000 แต้ม";
		close2;
		dispbottom "คุณมี Mission Points ทั้งหมด "+ #Mission_Points +" แต้ม";
		callshop "mission_shop",1;
		npcshopattach "mission_shop";
		break;
//	case 6:
//		mes "[Hunting Missions]";
//		mes "";
//		mes "โอเค ถ้าจะดูการแลกเปลี่ยนอื่น ๆ ก็จัดไปสิ แต่ที่นี่เรารับแลกแต่ไอเท็มที่มีชื่อว่า ^0055FFHunting's Coin [ItemID: 9955504]^000000 เท่านั้นนะจ๊ะ อย่าหาเอาไอเท็มอื่นมาแลกหล่ะ";
//		close2;
//		callshop "mission_exchange";
//		break;
	case 6:
		mes "^AA00FFรายชื่อนักล่ามือฉมัง^000000:";
		mes "";
		query_sql("SELECT char_id AS id, (SELECT `name` FROM `char` WHERE char_id = id),`value` FROM `char_reg_num` WHERE `key` = 'Mission_Total' ORDER BY CAST(`value` AS SIGNED) DESC LIMIT 5", .@id, .@name$, .@val);
		for (.@i = 0; .@i < 10; .@i++)
			mes "  [Rank " + (.@i+1) + "]  " + ((.@name$[.@i] == "") ? "^777777none" : "^0055FF" + .@name$[.@i]+"^000000 : ^FF0000" + .@val[.@i] + " Missions.") + "^000000";
		close;
	case 7:
		mes "[Monster Hunting]";
		mes "";
		mes "ไม่มีอะไรสินะ ข้าไปทำงานอื่นต่อหล่ะ";
		emotion ET_SCRATCH;
		close;
	}
	end;

Mission_Status:
	@f = false;
	deletearray .@j[0], getarraysize(.@j);
	for (.@i = 0; .@i < .Quests; .@i++) {
		.@j[.@i] = getd("Mission" + .@i);
		.@j[.Quests] = .@j[.Quests] + strmobinfo(3,.@j[.@i]);
		.@j[.Quests+1] = .@j[.Quests+1] + (strmobinfo(6,.@j[.@i]) / (getbattleflag("base_exp_rate") / 100) * .Modifier[0]);
		.@j[.Quests+2] = .@j[.Quests+2] + (strmobinfo(7,.@j[.@i]) / (getbattleflag("job_exp_rate") / 100) * .Modifier[1]);
		mes " > "+Chk(getd("Mission"+.@i+"_"),#Mission_Count) + strmobinfo(1,.@j[.@i]) + " (" + getd("Mission"+.@i+"_") + "/" + #Mission_Count + ")^000000";
	}

	// Reward formulas:
	.@Mission_Points = 5 + (.@j[.Quests] / .Quests / 5);
	.@Base_Exp = #Mission_Count * .@j[.Quests+1] / 2;
	.@Job_Exp = #Mission_Count * .@j[.Quests+2] / 2;
	.@Zeny = #Mission_Count * .Quests * .@j[.@i] * .Modifier[2];

	next;
	mes "[Monster Hunting]";
	mes "Mission rewards:";
	mes " > Mission Points: ^0055FF" + .@Mission_Points + "^000000";
	mes " > Base Experience: ^0055FF" + F_InsertComma(.@Base_Exp) + "^000000";
	mes " > Job Experience: ^0055FF" + F_InsertComma(.@Job_Exp) + "^000000";
	mes " > Zeny: ^0055FF" + F_InsertComma(.@Zeny) + "^000000";
	if (@f) {
		@f = false;
		return;
	}
	next;
	mes "[Monster Hunting]";
	mes "";
	mes "โอ้ว เจ้านี่ก็เก่งเหมือนกันนะ อ่ะ นี่คือรางวัลของเจ้า";
	emotion ET_BEST;
	specialeffect2 EF_ANGEL;
	specialeffect2 EF_TRUESIGHT;
	#Mission_Points += .@Mission_Points;
	BaseExp += .@Base_Exp;
	JobExp += .@Job_Exp;
	Zeny += .@Zeny;
	for (.@i = 0; .@i < .Quests; .@i++) {
		setd "Mission" + .@i, 0;
		setd "Mission" + .@i+"_", 0;
	}
	#Mission_Count = 0;
	if (.Delay)
		#Mission_Delay = gettimetick(2) + (.Delay * 3600);
	Mission_Total++;
	if (Mission_Total == 1)
		query_sql("INSERT INTO `char_reg_num` (`char_id`,`key`,`index`,`value`) VALUES (" + getcharid(0) + ",'Mission_Total','0',1)");
	else
		query_sql("UPDATE `char_reg_num` SET `value` = " + Mission_Total + " WHERE `char_id` = " + getcharid(0) + " AND `key` = 'Mission_Total'");
	close;

Mission_Info:
	mes "[Monster Hunting]";
	mes "";
	mes "หากเจ้าคิดจะล่ามอนสเตอร์เพื่อรับรางวัล เจ้ามาถูกที่แล้วหล่ะ ข้าจะมอบภารกิจที่หลากหลายให้เจ้า ของรางวัลก็ขึ้นอยู่กับความยาก ง่ายของมอนสเตอร์หล่ะนะ";
	next;
	mes "[Monster Hunting]";
	mes "";
	mes "^FF00001 ID รับภารกิจได้ 1 รอบเท่านั้น!!! รับซ้อนกันไม่ได้!!!^000000";
	if (.Delay)
	mes "เมื่ออยู่ในช่วงเตรียมตัวรอเช่นกัน เจ้าจะมารับ ก็ไม่ได้!!!";
	next;
	mes "[Monster Hunting]";
	mes "";
	mes "เพราะเมื่อเสร็จภารกิจ ทุกคนจะมีเวลาเตรียมตัวเพื่อรับภารกิจใหม่";
	mes (.Delay ? "ในทุก ๆ " + ((.Delay == 1) ? "ชั่วโมง" : .Delay + " ชั่วโมง") : "เท่ากันหมด");
	next;
	mes "[Monster Hunting]";
	mes "";
	mes "ส่วนของรางวัลที่สามารถแลกได้ ก็ลองกดเข้าไปดูละกันนะ ข้าไม่อยากอธิบายยืดยาว";
	return;

function Chk {
	if (getarg(0) < getarg(1)) {
		@f = true;
		return "^FF0000";
	} else
		return "^00FF00";
}

OnBuyItem:
	.@size = getarraysize(@bought_nameid);
	for (.@i = 0; .@i < .@size; .@i++) {
		.@j = inarray(.Shop, @bought_nameid[.@i]);
		.@cost += (.Shop[.@j+1] * @bought_quantity[.@i]);
	}
	mes "[Monster Hunting]";
	mes "";
	if (.@cost > #Mission_Points)
		mes "โอยยย... เจ้ามีแต้มไม่พอที่จะแลกของรางวัลจากข้านะ";
	else {
		for (.@i = 0; .@i < .@size; .@i++) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			dispbottom "Purchased " + @bought_quantity[.@i] + "x " + getitemname(@bought_nameid[.@i]) + ".";
		}
		#Mission_Points -= .@cost;
		mes "การแลกเปลี่ยนเรียบร้อย ขอบคุณ";
		emotion ET_MONEY;
	}
	deletearray @bought_nameid[0], .@size;
	deletearray @bought_quantity[0], .@size;
	close;

OnNPCKillEvent:
	if (!getcharid(1) || !.Party) {
		if (!#Mission_Count || !Mission0) end;
		for (.@i = 0; .@i < .Quests; .@i++) {
			if (strmobinfo(1,killedrid) == strmobinfo(1,getd("Mission" + .@i))) {
				if (getd("Mission" + .@i + "_") < #Mission_Count) {
					dispbottom "[Hunting Mission] Killed " + (set(getd("Mission" + .@i + "_"),getd("Mission" + .@i + "_") + 1)) +
					           " of " + #Mission_Count + " " + strmobinfo(1,killedrid) + ".";
					end;
				}
			}
		}
	} else if (.Party) {
		.@mob = killedrid;
		getmapxy(.@map1$,.@x1,.@y1);
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		for (.@i = 0; .@i < $@partymembercount; .@i++) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i])) {
				set .@Mission_Count, getvar(#Mission_Count, $@partymembercid[.@i]);
				set .@Mission0, getvar(Mission0, $@partymembercid[.@i]);
				set .@HP, readparam(HP, $@partymembercid[.@i]);

				if (.@Mission_Count && .@Mission0 && .@HP > 0) {
					getmapxy(.@map2$,.@x2,.@y2,BL_PC,rid2name($@partymemberaid[.@i]));
					if ((.@map1$ == .@map2$ || .Party == 1) && (distance(.@x1,.@y1,.@x2,.@y2) <= 30 || .Party < 3)) {
						for (.@j = 0; .@j < .Quests; .@j++) {
							.@my_mob_id = getvar( getd("Mission"+.@j),$@partymembercid[.@i] );
							.@my_count = getvar( getd("Mission"+.@j+"_"), $@partymembercid[.@i] );
							if (strmobinfo(1,.@mob) == strmobinfo(1,.@my_mob_id)) {
								if (.@my_count < .@Mission_Count) {
									setd "Mission"+.@j+"_", (.@my_count+1), $@partymembercid[.@i];
									dispbottom "[Hunting Mission] Killed " + (.@my_count+1) + " of " + .@Mission_Count + " " + strmobinfo(1,.@mob) + ".", 0x777777, $@partymembercid[.@i];
									break;
								}
							}
						}
					}
				}
			}
		}
	}
	end;

OnInit:
	waitingroom "Monster Hunting",0;
	.Delay = 4;            // Quest delay, in hours (0 to disable).
	.Quests = 4;            // Number of subquests per mission (increases rewards).
	.Party = 3;             // Party options: 0 (exclude party kills), 1 (include party kills), 2 (same map only), 3 (screen area only)
	.Reset = 100000;            // Reset options: -1 (abandoning mission sets delay time), 0 (no delay time), [Zeny] (cost to abandon mission, no delay time)
	setarray .Count[0],     // Min and max monsters per subquest (increases rewards).
		50,100;
	setarray .Modifier[0],  // Multipliers for Base Exp, Job Exp, and Zeny rewards.
		getbattleflag("base_exp_rate")/3,getbattleflag("job_exp_rate")/3,5;
	.mob_db$ =              // Table name of SQL mob database
		(checkre(0))?"mob_db_re":"mob_db";
	setarray .Shop[0],      // Reward items: <ID>,<point cost> (about 10~20 points per hunt).).
		9955504,1,
		19168,50,
		15186,50,
		19095,50,
		15145,50,
		19163,50,
		5208,50;
	.Blacklist$ =           // Blacklisted mob IDs.
		"3505,3801,1087,1147,1190,1086,1115,1038,1511,3901,"+
		"1159,1389,1046,1059,1150,1688,1039,1157,1980,3902,"+
		"1112,1251,2068,1373,2156,2194,1272,1630,1885,1252,"+
		"1779,1502,1708,1766,1767,2441,2442,1492,1623,2187,"+
		"2188,3658,3426,3427,1418,3428,3429,3430,1312,1583,"+
		"3628,3633,1785,3450,2022,3758,3621,2202,1734,3668,"+
		"1685,1719,3796,1871,1768,3757,2087,2165,1658,1751,"+
		"2249,2341,2253,2255,2362,20563,1832,2251,1874,2475,"+
		"2476,1917,2483,2189,2190,2529,2532,2533,2534,2535,"+
		"3741,1646,1647,1648,1649,1650,1651,2235,2236,2237,"+
		"2238,2239,2240,2241,2996,3473,3659,3254,3074,3150,"+
		"3151,3241,3245,3246,3221,3223,3224,3240,3242,3243,"+
		"3244,20419,3220,3222,3225,20422,20421,21537,21904,"+
		"21950,21955";

	npcshopdelitem "mission_shop",512;
	for (.@i = 0; .@i < getarraysize(.Shop); .@i += 2)
		npcshopadditem "mission_shop", .Shop[.@i], .Shop[.@i+1];
	end;
}
-	shop	mission_shop	-1,512:-1